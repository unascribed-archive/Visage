plugins {
    id 'java'
	id 'application'
	
    id 'eclipse'
    id 'idea'

    id 'com.stehno.natives' version '0.2.3'
    id 'com.github.hierynomus.license' version '0.11.0'
}

defaultTasks 'clean', 'licenseFormat', 'build'

// Project information
group = 'com.gameminers'
version = '1.1.1-SNAPSHOT'
archivesBaseName = 'Visage'
mainClassName = 'com.gameminers.visage.Visage'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
    lwjgl = '2.9.3'
}

// Project repositories
repositories {
    mavenCentral()
	maven {
		name 'gameminers'
		url 'http://mvn.gameminers.com/artifactory/repo'
	}
}

// Project dependencies
dependencies {
    compile 'com.google.guava:guava:18.0'
    compile "org.lwjgl.lwjgl:lwjgl:$lwjgl"
    compile "org.lwjgl.lwjgl:lwjgl_util:$lwjgl"
    compile 'net.sf.jopt-simple:jopt-simple:4.5'
	compile 'org.eclipse.jetty:jetty-server:9.3.0.M2'
	compile 'com.typesafe:config:1.2.1'
	compile 'com.rabbitmq:amqp-client:3.5.1'
	compile 'com.google.code.gson:gson:2.2.4'
	compile 'org.fusesource.jansi:jansi:1.11'
	compile 'org.spacehq:mcauthlib:1.0-gm-fixed'
	compile 'redis.clients:jedis:2.6.2'
}

// Read source files using UTF-8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation = true
}

natives {
    jars = configurations.compile.collectMany {
        it.name.contains('natives') ? [it.name] : []
    }
}

run {
	systemProperty 'java.library.path', file('build/natives/' + com.stehno.gradle.natives.Platform.current().os)
}

startScripts {
	defaultJvmOpts = [ '-Djava.library.path=./lib/natives/' ]
	classpath = classpath.filter {
		!it.name.contains('natives')
	}
}
applicationDistribution.from("conf") {
    into "conf"
}
applicationDistribution.from("www") {
	into "www"
}
applicationDistribution.from(unpackNatives) {
	from fileTree('build/natives').files
	into 'lib/natives'
}
applicationDistribution.exclude {
	it.name.contains('natives') && it.name.endsWith('.jar')
}

// License header formatting
license {
	exclude 'src/main/java/org/spacehq/mc/auth/SessionService.java'
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
